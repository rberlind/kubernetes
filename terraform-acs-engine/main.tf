terraform {
  required_version = ">= 0.10.1"
}

module "ssh_key" {
  source = "github.com/hashicorp-modules/ssh-keypair-data.git"
  private_key_filename = "${var.private_key_filename}"
}

resource "null_resource" "save_ssh_keys" {
  provisioner "local-exec" {
    command = "echo \"${chomp(module.ssh_key.private_key_pem)}\" > ${var.private_key_filename}"
  }

  provisioner "local-exec" {
    command = "chmod 600 ${var.private_key_filename}"
  }

  provisioner "local-exec" {
    command = "echo \"${chomp(module.ssh_key.public_key_data)}\" > ${var.public_key_openssh_filename}"
  }

  provisioner "local-exec" {
    command = "chmod 600 ${var.public_key_openssh_filename}"
  }
}

provider "azurerm" {
  subscription_id = "${var.azure_subscription_id}"
  tenant_id       = "${var.azure_tenant_id}"
  client_id       = "${var.azure_client_id}"
  client_secret   = "${var.azure_client_secret}"
}

# Azure Resource Group
resource "azurerm_resource_group" "default" {
  name     = "${var.resource_group_name}"
  location = "${var.azure_location}"
}

# Azure Virtual Network
resource "azurerm_virtual_network" "default" {
  name                = "${var.virtualnetworkname}"
  address_space       = ["${var.cidr}"]
  location            = "${var.azure_location}"
  resource_group_name = "${var.resource_group_name}"
}

# Azure Virtual Network -> Subnet
resource "azurerm_subnet" "default" {
  name                 = "${var.virtualnetworkname}_subnet"
  resource_group_name  = "${var.resource_group_name}"
  virtual_network_name = "${azurerm_virtual_network.default.name}"
  address_prefix       = "${var.cidr_subnet}"
  depends_on = ["azurerm_virtual_network.default"]
}

# ACS Engine Config
data "template_file" "acs_engine_config" {
  template = "${file(var.acs_engine_config_file)}"

  vars {
    master_vm_count = "${var.master_vm_count}"
    dns_prefix = "${var.dns_prefix}"
    vm_size = "${var.vm_size}"
    subnet_id = "${azurerm_subnet.default.id}"
    first_master_ip = "${var.first_master_ip}"
    worker_vm_count = "${var.worker_vm_count}"
    admin_user = "${var.admin_user}"
    # Need to trim linebreak at end of the public key generated by
    # the ssh-keypair-data module with chomp function
    ssh_key = "${chomp(module.ssh_key.public_key_data)}"
    service_principal_client_id = "${var.azure_client_id}"
    service_principal_client_secret = "${var.azure_client_secret}"
  }

  depends_on = ["azurerm_subnet.default"]
}

# Locally output the rendered ACS Engine Config (after substitution has been performed)
resource "null_resource" "render_acs_engine_config" {
  provisioner "local-exec" {
    command = "echo '${data.template_file.acs_engine_config.rendered}' > ${var.acs_engine_config_file_rendered}"
  }

  depends_on = ["data.template_file.acs_engine_config"]
}

# Locally run the ACS Engine to produce the Azure Resource Template for the K8s cluster
resource "null_resource" "run_acs_engine" {
  provisioner "local-exec" {
    command = "acs-engine generate ${var.acs_engine_config_file_rendered}"
  }

  depends_on = ["null_resource.render_acs_engine_config"]
}

# Locally run the Azure 2.0 CLI to create the resource deployment
resource "null_resource" "deploy_acs" {
  provisioner "local-exec" {
    command = "az group deployment create --name ${var.cluster_name} --resource-group ${var.resource_group_name} --template-file ./$(find _output -name 'azuredeploy.json') --parameters @./$(find _output -name 'azuredeploy.parameters.json')"
  }

  depends_on = ["null_resource.run_acs_engine"]
}

# Locally run the Azure 2.0 CLI to fix the routes
resource "null_resource" "fix_routetable" {
  provisioner "local-exec" {
    command = "az network vnet subnet update --name ${azurerm_subnet.default.name} --resource-group ${var.resource_group_name} --vnet-name ${azurerm_virtual_network.default.name} --route-table $(az resource list --resource-group ${var.resource_group_name} --resource-type Microsoft.Network/routeTables | jq -r '.[] | .id')"
  }

  depends_on = ["null_resource.deploy_acs"]
}
